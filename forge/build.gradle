plugins {
    alias(libs.plugins.forge)
}

minecraft {
    mappings channel: "official", version: "${libs.versions.minecraftVersion.get()}"
    copyIdeResources = true

    runs {
        configureEach {
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "error"
        }

        client {
            taskName "runOMForgeClient"
            workingDirectory project.file("./run/client")
        }

        server {
            taskName "runOMForgeServer"
            workingDirectory project.file("./run/server")
            args "--nogui"
        }

        gameTestServer {
            taskName "runOMForgeTestServer"
            workingDirectory project.file("./run/test")
            property "forge.enabledGameTestNamespaces", "overpoweredmending"
        }

        data {
            taskName "runOMForgeData"
            workingDirectory project.file("./run/data")
            args '--mod', 'overpoweredmending', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

configurations {
    library
    implementation.extendsFrom library
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft(libs.forge)
    library(project(":common"))

    implementation(fg.deobf("top.theillusivec4.curios:curios-forge:${libs.versions.curiosVersion.get()}:api"))
}

ext {
    minecraftVersion = libs.versions.minecraftVersion.get()
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from "src/main/resources", "../common/src/main/resources"

    filesMatching(["META-INF/mods.toml", "pack.mcmeta"]) {
        expand project.properties
    }
}

jar.finalizedBy("reobfJar")
